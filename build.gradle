plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '1.5.5'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.tcoded'
version = '1.0.2'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.20.1-R0.1-SNAPSHOT")
    compileOnly "org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT"
//    compileOnly "com.github.Hex27:TerraformGenerator:-SNAPSHOT"
    implementation files("${System.getProperty("user.dir")}/lib/TerraformGenerator.jar")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set("")
    relocate 'org.terraform', 'com.tcoded.terragenbiomesmockregistry.lib.terraform'
}

reobfJar {
    dependsOn("shadowJar")
}
